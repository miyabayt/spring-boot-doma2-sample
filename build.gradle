buildscript {
    dependencies {
        classpath "com.google.cloud.tools:jib-spring-boot-extension-gradle:0.1.0"
    }
}

plugins {
    id "idea"
    id "org.springframework.boot" version "${springBootVersion}" apply false
    id "org.owasp.dependencycheck" version "9.0.2" apply false
    id "org.domaframework.doma.compile" version "2.0.0" apply false
    id "com.google.cloud.tools.jib" version "3.2.1" apply false
    id "com.avast.gradle.docker-compose" version "0.16.13" apply false
    id "com.diffplug.spotless" version "${spotlessVersion}"
    id "com.github.ben-manes.versions" version "0.50.0" apply false
    id "jacoco"
}

apply plugin: com.bigtreetc.sample.CodeGenPlugin
apply plugin: "io.spring.dependency-management"
apply plugin: "docker-compose"

subprojects {
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.owasp.dependencycheck"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: "jacoco"

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/" }
    }

    spotless {
        java {
            googleJavaFormat()
            trimTrailingWhitespace()
            endWithNewline()
        }

        groovy {
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
        }

        groovyGradle {
            target "*.gradle", "**/*.gradle"
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
        }

        format "misc", {
            target "**/*.md", "**/*.yml"
            trimTrailingWhitespace()
            indentWithSpaces(2)
            endWithNewline()
        }

        lineEndings "UNIX"
    }

    sourceCompatibility = "17"
    [compileJava, compileTestJava, compileGroovy, compileTestGroovy]*.options*.encoding = "UTF-8"
    [compileJava, compileTestJava, compileGroovy, compileTestGroovy]*.options*.compilerArgs = ["-parameters", "-Xlint:all"]

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"

        testImplementation "org.springframework.security:spring-security-test"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.spockframework:spock-core:${spockVersion}"
        testImplementation "org.spockframework:spock-spring:${spockVersion}"

        runtimeOnly "org.springframework.boot:spring-boot-properties-migrator"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = "full"
            showCauses = true
            showExceptions = true
            showStackTraces = true
        }
        finalizedBy jacocoTestReport // report is always generated after tests run
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }

    compileJava.dependsOn "spotlessApply"
}

codegen {
    domainProjectName = "sample-domain"
    webProjectName = "sample-web-admin"

    commonDtoPackageName = "com.bigtreetc.sample.domain.entity.common"
    daoPackageName = "com.bigtreetc.sample.domain.dao"
    dtoPackageName = "com.bigtreetc.sample.domain.entity"
    repositoryPackageName = "com.bigtreetc.sample.domain.repository"
    servicePackageName = "com.bigtreetc.sample.domain.service"
    commonServicePackageName = "com.bigtreetc.sample.domain.service"
    exceptionPackageName = "com.bigtreetc.sample.domain.exception"
    webBasePackageName = "com.bigtreetc.sample.web.base"
    baseValidatorPackageName = "com.bigtreetc.sample.domain.validator"
    baseControllerPackageName = "com.bigtreetc.sample.web.base.controller.html"
    controllerPackageName = "com.bigtreetc.sample.web.admin.controller"
}
