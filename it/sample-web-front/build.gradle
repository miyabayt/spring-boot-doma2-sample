ext {
    drivers = ["dockerChrome", "dockerFirefox"]
}

bootJar {
    enabled = false
}

jar {
    enabled = false
}

dependencies {
    testImplementation project(":sample-web-front")

    testImplementation "org.gebish:geb-spock:${gebVersion}"
    testImplementation "org.seleniumhq.selenium:htmlunit-driver:3.62.0"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:${seleniumVersion}"
    testImplementation "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
    testImplementation "org.seleniumhq.selenium:selenium-api:${seleniumVersion}"
    testImplementation "org.testcontainers:spock:1.17.3"
    testImplementation "org.testcontainers:selenium:1.17.3"
    testImplementation "org.testcontainers:mysql:1.17.3"
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        group JavaBasePlugin.VERIFICATION_GROUP
        outputs.upToDateWhen { false }  // Always run tests
        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}
